(** {1 Transformer}

    This [Transformer] module holds different markdown to markdown
    transformations (if you can think of more please add them). This is a nice
    way to extend [Omd]'s capabilities in a simple, non-intrusive way.

    {2 Table of Contents}

    The [Toc] module does what it says on the tin. It takes markdown document,
    extracts the headers and produces a table of contents based on those headers
    that you can add and style as you see fit. You'll need to see the generated
    toc or have a look at the source code to find what classes to style in your
    CSS... Or you could use the low-level tools and write your own [to_html]
    function *)

module Toc : sig
  type t = heading list

  and heading = H of int * string

  val toc : Omd.doc -> t

  type 'a tree = Br of 'a * 'a tree list

  val to_tree : t -> heading tree

  val transform : Omd.doc -> Omd.doc

  val preorder :
    heading tree -> [< Html_types.li_content_fun > `A `Ul ] Tyxml.Html.elt

  val to_html : t -> [> Html_types.details ] Tyxml.Html.elt
end

(** {2 Images}

    The images transformer uses the {!Sesame.Responsive} module under-the-hood
    to provide easy-to-add responsive image handling for markdown documents. It
    comes with the fairly big caveat that the images in the markdown document
    should be specified relative to that document as it made path resolution
    much easier.

    It will rewrite any occurence of [!\[text\](./image/path)] into a responsive
    image HTML tag with srcset generated by the user supplying a
    {!Sesame.Responsive.Images.t}*)

module Image : sig
  type t
  (** Some configuration for the images being transformed in a markdown file *)

  val v : quality:int -> path:Fpath.t -> dst:Fpath.t -> Responsive.Images.t -> t
  (** [v ~quality ~dst responsive] is a constructor for this transform's
      configuration value, images are saved at [quality] and into [dst]. The
      [responsive] value tells the transformer what sizes to make *)

  val transform : t -> Omd.doc -> Omd.doc
  (** [transform t doc] transforms all of the images in [doc] as specified by
      the configuration *)
end
